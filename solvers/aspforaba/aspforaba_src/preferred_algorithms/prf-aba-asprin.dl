%% Assumption is either in or out
in(X) :- assumption(X), not out(X).
out(X) :- assumption(X), not in(X).

%% Everything derivable from assumptions that are in
supported(X) :- assumption(X), in(X).
supported(X) :- head(R,X), triggered_by_in(R).
triggered_by_in(R) :- head(R,_), supported(X) : body(R,X).

%% conflict freeness
:- in(X), contrary(X,Y), supported(Y). % This is actually unnecessary? Checked in admissibility?

%% assumptions attacked by the in arguments
defeated(X) :- supported(Y), contrary(X,Y).

%% Everything derivable from assumptionpts that are not defeated by in
derived_from_undefeated(X) :- assumption(X), not defeated(X).
derived_from_undefeated(X) :- head(R,X), triggered_by_undefeated(R).
triggered_by_undefeated(R) :- head(R,_), derived_from_undefeated(X) : body(R,X).

%% X is attacked by assumptionpts that are not attacked by in
attacked_by_undefeated(X) :- contrary(X,Y), derived_from_undefeated(Y).

%% admissibility
:- in(X), attacked_by_undefeated(X).

%% completeness (this is optional, but might make computatation faster)
:- out(X), not attacked_by_undefeated(X).

%% Skeptical reasoning.
%% Looking for a counterexample, i.e. assumption set not supporting the query
query :- not supported(X), query(X).

#show in/1.
#show supported/1.

% preferred via asprin
#preference(p1,superset) {in(X) : assumption(X) }.
#optimize(p1).
