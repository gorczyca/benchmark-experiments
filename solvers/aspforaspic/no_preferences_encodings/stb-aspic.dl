%% Axioms and a subset of premises are in
in(X) :- axiom(X).
in(X) :- premise(X), not out(X).
out(X) :- premise(X), not in(X).

%% Strict rules and a subset of defeasible rules are in
in(R) :- strict_head(R,_).
in(R) :- head(R,_), not out(R).
out(R) :- head(R,_), not in(R).

%% Everything derivable from premises that are in (+ axioms) using rules that are in and strict rules
supported(X) :- axiom(X).
supported(X) :- premise(X), in(X).
supported(X) :- head(R,X), triggered_by_in(R).
supported(X) :- strict_head(R,X), triggered_by_in(R).
triggered_by_in(R) :- in(R), head(R,_), supported(X) : body(R,X).
triggered_by_in(R) :- strict_head(R,_), supported(X) : strict_body(R,X).

%% reduce redundancy
:- in(R), not triggered_by_in(R), head(R,_).

%% premises and defeasible rules attacked by the in arguments
defeated(X) :- supported(Y), contrary(X,Y), premise(X).
defeated(X) :- supported(Y), contrary(X,Y), head(X,_).

%% defeasible rules whose conclusion is attacked by in
defeated(X) :- head(X,S), supported(Y), contrary(S,Y).

%% Conflict-freeness (for both premises and rules)
:- in(X), defeated(X).

%% Everything derivable from undefeated premises (+ axioms) using undefeated rules and strict rules
supported_by_undefeated(X) :- axiom(X).
supported_by_undefeated(X) :- premise(X), not defeated(X).
supported_by_undefeated(X) :- head(R,X), triggered_by_undefeated(R).
supported_by_undefeated(X) :- strict_head(R,X), triggered_by_undefeated(R).
triggered_by_undefeated(R) :- not defeated(R), head(R,_), supported_by_undefeated(X) : body(R,X).
triggered_by_undefeated(R) :- strict_head(R,_), supported_by_undefeated(X) : strict_body(R,X).

%% no undefeated sentences outside in can be derived
:- out(X), not defeated(X), premise(X).

% there can not be rules outside in that are usable but unattacked rules
:- triggered_by_undefeated(R), out(R).

%% credulous
:- not supported(X), query(X).

%% skeptical
%:- supported(X), query(X).

inrule(X) :- head(X,_), in(X).
inpremise(X) :- premise(X), in(X).

%#show in/1.
#show inrule/1.
#show inpremise/1.
#show supported/1.
%#show triggered_by_in/1.
